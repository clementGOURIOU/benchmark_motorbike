#
# Automatic post processing by Hakan Nilsson, Maryse Page and Martin Beaudoin
#
# Modified for the compatibility with gnuplot 5.2 by
#     Sergey Lesnik, Wikki GmbH, Germany, 2021
#
# ARG0: 1st argument, position
# ARG1: 2st argument, angle of diffuser in degrees
# ARG2: 3rd argument, radius at cross-section
# ARG3: 4th argument, case name of solution 1
# ARG4: 5th argument, time of sample for solution 1
#

pause 0 sprintf("arg0 : %s", ARG0)
pause 0 sprintf("arg1 : %s", ARG1)
pause 0 sprintf("arg2 : %s", ARG2)
pause 0 sprintf("arg3 : %s", ARG3)
pause 0 sprintf("arg4 : %s", ARG4)
pause 0 sprintf("arg5 : %s", ARG5)

angle = ARG2*3.141592654/180.  # angle of diffuser in radians
U0    = 11.6                 # axial mean velocity [m/s]

set output sprintf('U%s.png', ARG1)
set terminal png medium
#set output 'UARG0.eps'
#set term postscript color  # color
#set term postscript        # black and white
#Enhanced Metafile Format, for vectorized MS Win32 plots:
#set terminal emf monochrome dashed 'Times' 20
#set output 'UARG0.emf'

#
# ERCOFTAC TESTCASE 3: Swirling boundary layer in conical diffuser
#
# U/(U0) as a function of y (m), at corresponding sections
#

set autoscale
set noarrow
set nolabel
set nogrid
set grid

set xlabel 'y (mm)'
set ylabel 'U/(U_0)'
set title sprintf('Section z = %smm', ARG1)
set xrange [0:220]
set xtics 0,20,220
set yrange [-0.2:1.3]
set ytics -0.2,0.1,1.3

#
plot \
     sprintf("../../measurements/test60/Mm%s.dat", ARG1) \
     using (column(1)):(column(2)/U0) title "Measured U" \
     with points linewidth 2 \
     , \
     sprintf("../../measurements/test60/Mm%s.dat", ARG1) \
     using (column(1)):(column(3)/U0) title "Measured W" with points linewidth 2 \
     , \
     sprintf("./sampleDict/%s/Comp%s_Y_U.xy", ARG5, ARG1) \
     using ((ARG3-column(1))*1000/cos(angle)):\
     ((column(4)*cos(angle)+column(3)*sin(angle))/U0) \
     title sprintf("%s, U", ARG4) with lines linewidth 2 \
     , \
     sprintf("./sampleDict/%s/Comp%s_Y_U.xy", ARG5, ARG1) \
     using ((ARG3-column(1))*1000/cos(angle)):(-column(2)/U0) \
     title sprintf("%s, W", ARG4) with lines linewidth 2 \
